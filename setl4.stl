*   Copyright 2017, David Shields
*   Licensed under the MIT license.

*TODO - need separate out iterator from set data structure. May have multiple loops over same set at onece.

*   SETL4 is an extension of SPITBOL. This file consists of a set of functions
*   operator definitions that support set-theoretic operations defined
*   on finite sets.

*   SETL4 extends SPITBOL by adding the datatype _set_ to represent
*   finite sets. Several additional datatypes based on sets are
*   also provided. Each is identified by its kind.


*   SETL4 supports a variety of sets. Each has a kind, one of:

*       int   map   seq   set   str

*   For each kind, SETL4 provides set-theoretic functions,
*    as well as other functions that have proven to be useful.

*   SETL4 unary operators:

*   '='     =s      Iteration                   Returns the current element of an iteration
*   '|'     |'str'  Constructor                 Returns set specified by string _str_
*   '!'     !expr   Not                         True if and only if _expr_ is false
*   '#'     #s      Size                        Number of elements in set _s_
*   '/'     /s      Split                       Splits a string into a sequence of words

*   SETL4 binary operators:

*   '&'     a & b   And                         True if and only if both _a_ and _b_ are true
*   '@'     s @ e   Eval                        Evaluates _e_ at current element of set _s_
*   '#'     s # e   Filter                      Returns subset of set _s_ for which _e_ is true
*   '~'     m ~ s   Membership                  Tests if _m_ is member of set _s_
*   '^'     a ^ b   Or                          True if and only if _a_ or _b_ is true

*   For example, the folowing SETL4 expression determines if integer _P_ is prime:

*           !exists(|'int 2 P' @ 'multiple(P,this)')

*   where _multiple(a,b) is true if and only if _a_ is a multiple of _b_.

*   Global variables
*   ----------------

    setl4.random_var =

*   SETL4 binary operators:

*    setl4.ascii.name = new('map')
*    setl4.ascii.val =  new('map')
*    setl4.ascii.initialized = false

*   Boolean Logic
*   -------------

*   Boolean logic is an algebra with just two values: true and false.

*   In SPITBOL, the algebra is defined on strings: the null string is "false" 
*   and all other strings are "true".  The SPITBOL function IDENT is used 
*   to test for true, DIFFER for false. 
*   Each signals their result by success (true) or failure (false).

*   In SETL4, the algebra is defined both on integers and non-integers.  
*   For integers, zero is "false" and all other integers are "true." 
*   For non-integers, the null string ('') is true, and all other values are false.

*   SPITBOL comparison functions report their results by success or failure, so SETL
*   provides the following functions which return TRUE or FALSE.



*   String operations based on SPITBOL
*   ----------------------------------

*   str.any(a,b)                                Tests if the first character of _a_ is in _b_
*   str.break(a,b)                              Tests if _a_ contains one of the characters in _b_
*   str.notany(a,b)                             Tests if first character in _a_ is not in _b_
*   str.span(a,b)                               Tests if _b_ contains only characters in _a_

*   false(e)                                    Returns TRUE if and only if _e_ is FALSE
*   true(e)                                     Returns TRUE if and only if _e_ is TRUE
*   assert.type(obj,type)                         Tests that _obj_ has SPITBOL datatype _type_, ends execution otherwise
*   assert(expr)                                Tests that _expr_ is true, ends execution otherwise
*   exists(set,expr)                            Tests if _expr_ is true for at least one element in set _set_
*   forall(set,expr)                            Tests if _expr_ is true for at all elements in set _set_


*   Int: sets of kind _int_
*   ----------

*   A set of kind _int_ represents a set of integers. The set cannot be changed
*   once it is created. You can only loop over a set of kind _int_ or test for 
*   membership.

*   Int set functions
*   -----------------


*   Integer comparisons
*   -------------------

*   int.eq(a,b)                                 Tests if _a is equal to _b_
*   int.ge(a,b)                                 Tests if _a is greater than or equal to _b_
*   int.gt(a,b)                                 Tests if _a is greater than _b_
*   int.le(a,b)                                 Tests if _a is less than or equal to _b_
*   int.lt(a,b)                                 Tests if _a is less than _b_
*   int.ne(a,b)                                 Tests if _a is not equal to _b_

*   Int Utility Functions
*   ---------------------

*   int.cardinal(n,word)                        Returns 'one ' followed by word if _n_ is one, else returns '_n_ words'
*   int.even(n)                                 Tests if _n_ is even
*   int.factorial(n)                            Returns n!  = n * (n -1) * ... * 1
*   int.number(n)                               Returns _n_ as one or two words if possible, else returns _n_
*                                               For example, _int.number(51)_ is "fifty one".
*   int.multiple(n,m)                           Tests if _n_ is a multiple of _m_
*   int.even(n)                                 Tests if _n_ is odd
*   int.prime(n)                                Tests if _n_ is prime
*   int.square.root(n)                          Returns integer square root on _n_
*   int.thousands(s)

*   loop(set)                                   Set up iteration over _set_ using _next_


*   Map: sets of kind 'map'
*   --------
*   A map is a set of ordered pairs.

*   Map set functions
*   -----------------


*   map.domain(map)                             Returns set of elements in the domain of _map_
*   map.first(map)                              Returns the first element in a map
*   map.get(map,key)                            Gets the value of map _map_ for _key_
*   map.last(map)                               Returns the last element in a map.
*   map.put(map,key,val)                        Sets the value of map _map_ for key _key_ to be _value_
*   map.range(map)                              Returns set of elements in the range of _map_
*   map.defined(map,key)                        Tests if map _map_ if defined for _key_


*   mark(line)
*   next(set)                                   Returns the next iterate in loop over _set_, fails if no more remain
*   rank(set)                                   Returns the rank, or normal form, of a set or string.
*                                               For example, _rank('albuquerque')_ is 'abeelqqruuu';
*                                               the rank of a map is the map with keys sorted in ascending order.

*   Seq: set of kind _seq_

*   A sequence (_seq_) is a map on the positive integers 1 .. n
*   Sequences are implemented using SPITBOL arrays, and so are very efficient.


*   Seq set functions
*   -----------------

*   seq.add(seq,val)                            Adds _val_ to sequence _seq)
*   seq.get(seq,i)                              Returns value of element _i_ of sequence _seq_
*   seq.integers(n)                             Returns sequence consiting of integers 1 .. n
*   seq.push(seq,val)                           Adds _val to sequence _seq_
*   seq.pop(seq)                                Removes last element of sequence _seq_ and returns its value
*   seq.put(seq,i,val)                          Sets value of elment _i_ in sequence _seq_ to _val)
*   seq.range(seq)                              Returns set of range values of sequence _seq

*   Set 
*   ---

*   Set set functions
*   -----------------

*   set.add(set,elem)                           Adds element _elem_ to set _set_ if not yet a member of the set.
*   set.difference(a,b)                         Returns set of members of set _a_ not in the set _b_
*   set.first(set)                              Returns first element in set
*   set.from(set,elem)                          Returns set consisting of members of _set_ except for_elem_
*   set.integers(n)                             Returns set of integers 1 ... n
*   set.intersection(a,b)                       Returns set of members which belong to both sets _a_ and _b_
*   set.last(set)                               Returns last element in set
*   set.subset(a,b)                             Tests if every member of _b_ is also a member of _a_
*   set.union(a,b)                              Returns set consisting of all the members of _a_ and _b_

*   Show: display SETL4 object

*   The function _show_ is used to represent the value of a SETL4 object
*   in a human-readable form. The result is written to standard output.
*   Objects are shown in the order defined by the function _rank_.

*   show(v,lvl)                                 Show value of _v_, with _lvl_ set non-zero when listing member of set
*   show.b(e)                                   Show value of _b_ as boolean (TRUE or FALSE)
*   show.line(line)                             Show string _line_ with spaces replaced by '.'
*   show.lines(lines,title)                     Show non-empty lines in array of lines, with title 'title'
*   show.quote(str)                             Show string with surrounding quotes 
*   show.words(words)                           Show a sequence of words, separated by space character

*   Str: set of characters

*   _Str_ is used to treat a character string as a set of characters.

*   Lexical comparisons
-   -------------------

*   str.leq(a,b)                                Tests if a is lexically equal to b
*   str.lge(a,b)                                Tests if a is lexically greater than or equal to b
*   str.lgt(a,b)                                Tests if a is lexically greater than b
*   str.lle(a,b)                                Tests if a is lexically less than or equal to b
*   str.llt(a,b)                                Tests if a is lexically less than b
*   str.lne(a,b)                                Tests if a is lexically not equal to b

*   Str utility functions
*   -----------------

*   str.append(str,w,ch)                        Appends _ch_ (or space if _ch_ is null) to _str_, then appends _w_
*   str.ascii(line)
*   str.ascii.init(init)
*   str.datename(date)                          Returns string based on current time suitable for use as filename
*   str.frequency(str)                          Returns map giving frequency of characters in _str_
*   str.digits()                                Returns list of digits: '0 1 2 .. 9'
*   str.lletters()                              Returns list of lower case letters : 'a b c ... z'
*   str.letters()                               Returns list of letters : 'a b c ... z A B ... Z''
*   str.lower(s)                                Returns _s_ with upper case letters replaced by lower case equivalent
*   str.less(str,sub)                           Removes the first instance of each character in _sub_ from _str_,
*                                               where _sub_ is a substr of _str_.
*   str.out(text1,text2,text3)                  Outputs _text1_, then _text2_ enclosed in '[]' if _text2_ not null,
*                                               then _text3_ if _text3_ is not null.
*   str.prefix(str,pre)                         Takes a list of space-separated words in _str_ and prefixes each
*                                               with the string _pre_
*   str.reader(filename,expr)                   Returns sequence of lines in file _filename_, using _expr_ (if given)
*                                               to filter out lines to be ignored.
*   str.reader.string(str,delim)                Like _str.reader(filename..)_ but reads lines from a string with lines
*                                               separated by delimiter _delim_
*   str.slice(str,first,last)                   Same as SPITBOL _substr(str,first,last)_
*   str.tokens(line)                            Returns sequence of the tokens in _line_
*   str.uletters()                              Returns list of upper case letters : 'A B C ... Z''
*   str.upper(s)                                Returns _s_ with lower case letters replaced by upper case equivalent
*   str.words(line)                             Returns list of words in _line_, where words are separated by one of
*                                               more spaces or tab characters
*   str.writer(lines)                           Writes sequence _lines_ to standard output.

*   visit(set,expr)                             Visits each element of set _set_ and evaluates _expr_ for that element

*   Setl4 functions

*   Functions with names starting with 'setl4' are internal to the implementation.
*   Most of them implement the SETL4 operator such  as "^" and "&".
*   You can access them directly, but do so at your peril.

*
*   and(a,b)                                    And: true if and only if _a_ and _b_ are true
*   binary.search(tbl,value)                    Binary search
*   concatenate(a,b)                            Concatenates strings _a_ and _b_
*   at(set,expr)                        Evaluates _expr_ for for current iterate of _set_
*   filter(set,expr)                            Filter:; returns subset of _set_ for which _expr_ is true
*   iskind(set,kind1,kind2,kind3,kind3,kind4)   Tests if the kind of set _set_ is one of the specfied kinds.
*   member(elem,set)                            Tests if _elem_ is a member of set _set_
*   new(str)                                    Returns new set specified by _st_
*   next(set)                                   Returns next element in iteration defined by prevous _loop_,
*                                               fails if no more elements
*   not(expr)                                   Not: true if and only if _expr_ is false
*   and(a,b)                                    Or: true if and only if _a_ of _b_ is true
*   random(n)                                   Returns random element
*   random.seed()                               Sets random number seed to initialize _random_
*   setl4.size(set)                             Returns number of elements in set _set_
*   setl4.traceoff()                            Turns off tracing
*   setl4.traceon()                             Turns on tracing

*   A set is defined by the following datatype:

    data('set(set.kind,set.index,set.key,set.size,set.next,set.loop,set.low,set.high,set.step,set.text,set.seq)')

*   where

*           _kind_          the kind of _set_: 'int', 'map', 'seq', 'set', or 'str'.

*           _index_         a SPITBOL table used to map the integer
*                           id assigned each element to the value of the element.

*           _key_           a SPITBOL table, used only for maps, to permit iteration

*           _size_          the current number of members.

*           _next_          the last value returned by the _next_ primitive
*                           when iterating over a set.

*           _text_          is the text of the set of kind _str_

*           _seq_           is the array used to maintain a sequence. _set.key_
*                           and _set.index_ are not used for sequences.

*   Note that the null value is not allowed as an element of a set, or the
*   value of a map entry, but can be used as the value of an element in a seq.

*   The _key_ table is needed to provide constant time bounds for iterating over
*   a set, in all of its forms.

*   Since the _key_ table is needed, a set can be indexed using the key table.
*   This allows the definition of 'get' and 'put' on sequences.

*   Sequences are currently implemented using SPITBOL arrays, extending
*   the array length dynamically as needed.

*   New elements can be added to a sequence using the 'push' primitive,
*   and the most recent element can be deleted using the 'pop' primitive.

*   In order to avoid clashing with the names of user-defined functions, almost
*   all of the functions defined herein begin with one of the following strings:

*       'int'   'map'   'seq'   'set'   'setl4'    'str'

*   The following functions have no such prefix, due to the ubiquity of their use:

*       and  assert  exists false forall loop  mark  next  or  rank  show  true


*   SETL4 Configuration Options
*   ---------------------------

*   Initial size of any hash tables created when constructing
*   a new instance of a set.

*   Use large value for now, to avoid need for expansion during initial testing.
*    setl4.config.size = 16
    setl4.config.size = 120

*   SETL4 Global Variables
*   ----------------------


    setl4.break.whitespace = break(' ' char(9))
    setl4.span.whitespace =  span(' ' char(9))


*   SETL4 constants
*   ---------------

*   The values of the following variables should NEVER be changed, lest chaos ensue.

    FALSE = 0
    TRUE = 1

*   SETL4 function definitions
*   --------------------------

    define('and(a,b)')                              :(and.end)

*   Returns true iff (if and only if) both arguments are true.
*   Only evaluates _b_ if necessary

and

*   See if both operands are integers.

    and = FALSE
    and = integer(a) eq(a)                          :s(return)
    and = integer(a) integer(b) (ne(+a * +b) TRUE, FALSE)    :s(return)

*   Here where at least one operand for 'and' is not integer, so
*   use ident/differ to evaluate.

    and = differ(a) differ(b) true                  :s(return)
    and = FALSE                                     :(return)

and.end

    define('arb(set)n,r')                           :(arb.end)

*   Returns arbitary (randomly selected) element of _set_.

arb

    arb = integer(set) random(+set)           :s(return)
    r = random(set.size(set))
    arb = (
.       iskind(set,'int')         set.low(set) + (r - 1) * set.step(set),
.       iskind(set,'map','set')   set.index(set)[r],
.       iskind(set,'seq')         set.seq(set)[r],
.       iskind(set,'str')         substr(set.text(set),r,1),
.       ident(datatype(set),'string') substr(set,r,1))
                                                    :(return)
arb.end

    define('assert(expr)')                          :(assert.end)

*   Verify that _expr_ is TRUE, end execution otherwise.

assert

    true(expr)                                      :s(return)
    error('assertion failed, quitting') 

assert.end

    define('assert.type(obj,typ)')                  :(assert.type.end)

*   Verify that _obj_ has datatype _typ_, end execution if not.

assert.type

    assert(ident(datatype(obj),typ))                :s(return)
    error('assertion on datatype failed ' typ )
    

assert.type.end

    define('at(set,expr)this')             :(at.end)

*   Evaluate _expr_ 

at

    this = set.next(set)
    at = eval(expr)                         :(return)

at.end

    define('concatenate(a,b)')              :(concatenate.end)

*   Concatenate argument strings.

concatenate

    concatenate = a b :(return)

concatenate.end

    define('error(str)')                            :(error.end)

*   Report cause of error and quit execution

error

    &dump = 2
    output = 'Fatal error: ' str                    :(end)

error.end

    define('exists(set,expr)e')                     :(exists.end)

*   Tests if there is an element in _set such that _eval(expr)_ is TRUE.

exists

    loop(set)

exists.loop

    next(set)                                       :f(exists.fail)
*    str.out('exists.loop.next',set.next(set))
    e = eval(expr)                                  :f(exists.loop)
*    str.out('exists.loop e',e)
    integer(e) eq(+e)                               :s(exists.loop)
    ident(e) 				                        :s(exists.loop)
    exists = TRUE                                   :(return)

exists.fail

    exists = FALSE                                  :(return)

exists.end

    define('false(e)')                              :(false.end)

*   Returns TRUE if its argument is false, or else FALSE.

false

    e = integer(e) +e
    false = integer(e) (eq(e) TRUE,FALSE)           :s(return)
    false = (ident(e) TRUE, FALSE)                  :(return)

false.end

    define('filter(set,expr)e,this')           :(filter.end)

*   According to Paul Halmos, in his book "Naive Set Theory,"
*   a major principle of set theory, often referred to by its
*   German name _Aussonderungsaxiom_, is the Axiom of Specification:

*   To every set _A_ and every condition _S(x)_ there corresponds
*   a set _B_ whose elements are exactly those elements _x_ of _A_
*   for which _S(x)_ holds.

*   In SETL4 we say that the expression '_A_ @ _S_' "filters"
*   _A_ by _S_. This can also be read as
*           "the subset of _A_ such that _S_ is true"

*   Returns the subset of set or map _set_ for which _expr_ is true.

filter

    setl.filter = set.new(set.kind(set))

    loop(set)

filter.loop

    this = next(set)                                :f(return)
    e = eval(expr)                                  :f(filter.loop)
    false(e)                                        :s(set.4.filter.loop)
    iskind(set,'set') setl4.add(filter,this)        :(filter.loop)
    map.put(filter,key(this),value(this))           :(filter.loop)

filter.end

    define('forall(set,expr)e')                     :(forall.end)

*   Tests if _expr_ is true for all elements of _set_.

forall

    loop(set)

forall.loop

    next(set)                                       :f(forall.true)
*    str.out('forall.loop.next',set.next(set))
    e = eval(expr)                                  :f(forall.loop)
*    str.out('forall.loop',e)
    integer(e) eq(+e)                               :s(forall.false)
    ident(e)                                        :s(forall.false)
    						    :(forall.loop)

forall.false

    forall = FAlSE                                   :(return)

forall.true

    forall = TRUE				     :(return)

forall.end

*   Integers (int's)
*   --------

*   SETL4 provides the type _int_ .
*   An _int_ is a sequence of positive integers.
*
*   SETL4 extends the integer operations provided by SPITBOL by
*   providing the following integer functions:

*            int.even(n)       true iff _n_ is even.
*            int.factorial(n)  returns the value of
*                                   n! = _n_ * (n - 1) * ... *
*            int.multiple(n,m) true iff _n_ is multiple of _m_
*            int.odd(n)        true iff _n_ is odd.
*            int.prime(n)      true iff _n_ is a prime number
*            int.square.root   square root rounded up to next integer.

    define('int.int.cardinal(n,word)')           :(int.int.cardinal.end)
*   If N is one, returns 'one' followed by WORD.
*   Otherwise returns NUMBER(N) followed by WORD and 'S'.


int.int.cardinal

    int.cardinal = (eq(n,1) 'one ' word, number(n) ' '  word 's'):(return)

int.int.cardinal.end

    define('int.even(n)')                           :(int.even.end)

*   Returns TRUE iff _n_ is even.

int.even

    int.even = (eq(remdr(n,2)) true,false)          :(int.even.end)

int.even.end

    define('int.factorial(n)')                      :(int.factorial.end)

*   Returns n! = n*(n-1)*(n-2) ... 1

int.factorial

    int.factorial = (eq(n,1) 1, n * int.factorial(n -1))    :(return)

int.factorial.end

    define('int.multiple(n,m)')                      :(int.multiple.end)

*   Tests if _n_ is a multiple of _m_.

int.multiple

    int.multiple = (eq(remdr(n,m)) TRUE, FALSE)      :(return)

int.multiple.end

    define('int.number(n)')                          :(int.number.end)

*  If _n_ can be represented as a string of one or two words then return that string. Otherwise return _n_.

int.number
    error('int.number not implemented, quitting.' )
    int.number = eq(n) 'zero'                       :s(return)
    int.number = g.numbers[n]
    differ(int.number)                              :s(return)
    int.number = n                                  :(return)

int.number.end

    define('int.odd(n)')                            :(int.odd.end)

*   Returns true iff _n_ is odd.

int.odd

    int.odd = (ne(remdr(n,2)) TRUE,FALSE)           :(return)

int.odd.end


    define('int.prime(n)set')                       :(int.prime.end)

*   Returns true iff _n_ is prime.

int.prime

    set = new('int 2 ' square.root(p))
    int.prime = not(exists(set,'eq(remdr(p,=s))'))  :(return)

int.prime.end

    define('int.square.root(n)')                    :(int.square.root.end)

*   Returns square root of integer _n_, rounded up.

int.square.root

    int.square.root = convert(sqrt(convert(n,'real')),
.                               'integer') + 1 :(return)

int.square.root.end

    define('int.thousands(s)n')                  :(int.thousands.end)

*   Format s with comma's every three digits from right.

int.thousands

    n = size(s)
    int.thousands = le(n,3) s                    :s(return)
    int.thousands = int.thousands(substr(s,1,n - 3)) ',' substr(s, n - 2, 3):(return)

int.thousands.end

*   Integer Comparison Functions
*   --------------------

*   The following functions comparing integer values return TRUE or FALSE.
*   They are needed since SPITBOL signals the results of a comparison by success or failure.

    define('int.eq(a,b)')                           :(int.eq.end)

*   Tests if _a_ is equal to _b_.

int.eq

    int.eq = (integer(a) integer(b) eq(a,b) TRUE, FALSE) :s(return)

int.eq.end

    define('int.ge(a,b)')                           :(int.ge.end)

*   Tests if _a_ is greater than or equal to _b_.

int.ge

    int.ge = (integer(a) integer(b) ge(a,b) TRUE, FALSE) :s(return)

int.ge.end

    define('int.gt(a,b)')                           :(int.gt.end)

*   Tests if _a_ is greater than _b_.

int.gt

    int.gt = (integer(a) integer(b) gt(a,b) TRUE, FALSE) :s(return)

int.gt.end

    define('int.le(a,b)')                           :(int.le.end)

*   Tests if _a_ is less than or equal to _b_.

int.le

    int.le = (integer(a) integer(b) le(a,b) TRUE, FALSE) :s(return)

int.le.end

    define('int.lt(a,b)')                           :(int.lt.end)

*   Tests if _a_ is less than _b_.

int.lt

    int.lt = (integer(a) integer(b) lt(a,b) TRUE, FALSE) :s(return)

int.lt.end

    define('int.ne(a,b)')                           :(int.ne.end)

*   Tests if _a_ is not equal to _b_.

int.ne

    int.ne = (integer(a) integer(b) ne(a,b) TRUE, FALSE) :s(return)

int.ne.end

    define('iskind(set,kind1,kind2,kind3,kind3,kind4,kind5)kind')     :(iskind.end)

*   Tests if the kind of _set_ is one of kind1, kind2, .... Returns true if so,
*   false otherwise.

iskind

    kind = ident(datatype(set),'string') true       :s(return)
    kind = set.kind(set)
    iskind = differ(kind1) ident(kind,kind1) TRUE   :s(return)
    iskind = differ(kind2) ident(kind,kind2) TRUE   :s(return)
    iskind = differ(kind3) ident(kind,kind3) TRUE   :s(return)
    iskind = differ(kind4) ident(kind,kind4) TRUE   :s(return)
    iskind = differ(kind5) ident(kind,kind5) TRUE   :s(return)
    iskind = FALSE                                  :(return) 

iskind.end

    define('loop(set)dt')                           :(loop.end)

*   Set up for loop. Set set.next field so that on first call to _next()_
*   the index will be advanced to the first element in the set.

loop

*   Returns loop member in interation over compound object _set.
*	setl4.show(set)
    set.next(set) = ident(set.kind(set),'int') set.low(set) - set.step(set) :s(return)
    set.next(set) =  0                              :(return)

loop.end

*   Maps
*   --------------

*   A _pair_ joins two values together.

    data('pair(first,second)')

*   In mathematics, a map, or function, defined on a finite set is a set of 
*   ordered pairs that define a relation between the first element of a pair, 
*   its _key_, and the value of the map for _key_.

*   SETL4 represents a map entry as follows:

    data('entry(key,value)')

*   where _key_ is the key, and _value_ is the value of the
*   map for _key_.

*   The SPITBOL datatype TABLE provides the machinery to implement maps.
*   SPITBOL provides no way to determine the number of domain values,
*   or size, of a map other than converting the map to an array, which is
*   the only way to iterate over a set.

*   Each new entry added to a map is given an integer id,
*   starting with one.  The field _set.index_ maps the id's
*   to the associated entries.

*   The field _set.key_ maps the keys used in entries to
*   their associated id's, and is needed to allow constant-time
*   access to map entries, both when retrieving values and
*   when iterating over the set.

    define('map.defined(map,key)')                 :(map.defined.end)

*   Tests if the map _map_ has an entry for _key_.

map.defined

    map.defined = (differ(set.key(map)[key]) TRUE,FALSE) :(return)

map.defined.end

    define('map.domain(map)elem)')                 :(map.domain.end)

*   Return set of the keys in _map_.

map.domain

    map.domain = set.new()
    loop(map)

map.domain.next

    set.add(map.domain,key(next(map)))             :s(map.domain.next)f(return)

map.domain.end

    define('map.first(map)')                       :(map.first.end)

*   Returns first element in _map_.

map.first

    map.first = set.key(map)[1]                    :(return)

map.first.end

    define('map.get(map,key)val)id')                :(map.get.end)

*   Gets the value of the key _key_ in the map _map_.

map.get

    id = set.key(map)[key]
    ident(id)                                       :s(return)
    map.get = value(set.index(map)[id])             :(return)

map.get.end

    define('map.last(map)')                         :(map.last.end)

*   Returns last element in _map_.

map.last

    output = 'enter map.last'
    map.last = set.key(map)[set.size(map)]           :(return)

map.last.end

    define('map.put(map,key,value)v')               :(map.put.end)

*   Sets the value of the entry for key _key_ in map _map_ to be _val_,
*   creating a new entry if necessary.

map.put

    key = integer(key) +key
    value = integer(value) +value
    v = set.key(map)[key]
    differ(v)                                       :s(map.put.value)

*   Here to create new entry in map, and then merge to set new value.

    set.size(map) = set.size(map) + 1
    set.key(map)[key] = set.size(map)

map.put.value

    set.index(map)[set.key(map)[key]] = entry(key,value):(return)

map.put.end

    define('map.range(map)elem)')                  :(map.range.end)

*   Return set of the values in _map_.

map.range

    map.range = set.new()
    loop(map)

map.range.next

    set.add(map.range,value(next(map)))             :s(map.range.next)f(return)

map.range.end

    define('member(elem,set)kind,entry,i,key,this,value)') :(member.end)

*   Tests if _elem_ is 'elem' of _set, where:

*   _elem_ is element of 'int' if in range, and

*   _elem_ is element of 'int' if in range, and
*   meets requirement if 'step' in nonzero

*   _elem_ is element of map _set if _map_ has entry _elem_.

*   _elem_ is element of sequence _set if _set has entry _elem)

*   _elem_ is element of set _set if _set.in(elem,set)_ is true.

*   _elem_ is element of string _set if _set contains instance of _elem.

member

*    output = 'member ' datatype(set)
*    show(set)
    member = FALSE
    kind = set.kind(set)
    member = false
    iskind(kind,'int')                              :s(member.int)
    iskind(kind,'map')                              :s(member.map)
    iskind(kind,'seq')                              :s(member.seq)
    iskind(kind,'set')                              :s(member.set)
    iskind(kind,'str')                              :s(member.str)
    iskind(kind,'string')                           :s(member.string)
    iskind(kind,'map')                              :s(member.map)
    error('member wrong kind ' kind ' , quitting')

member.int

    member = lt(elem,set.low(set)) false              :s(return)
    member = gt(elem,set.low(set)) false              :s(return)
    member = (eq(remdr(elem - set.low(set),set.step(set))) TRUE, FALSE):(return)

member.map

    key = key(elem)

*   Cannot be member if no entry in _key_ table.

    ident(set.key(set)[key])                        :s(return)

*   There is entry for _key_, see if it has right value.

    equal(set.index[set.key(set)[key]],value)       :f(return)s(member.true)

member.seq

    gt(i = i + 1,set.size(set))                     :f(return)
    this = set.array(set)[i]                        :f(return)
    equal(this,elem)                                :s(member.true)f(member.seq) 

setl4.elem.set

    member = (differ(set.key(set)[elem]) TRUE, FALSE) :(return)

setl4.member.str

    set break(elem)                                 :f(return)s(member.true)

setl4.member.string

    string break(elem)                              :f(return)s(member.true)

member.true

    member = TRUE                                   :(return)

member.end


    define('new(init)kind,i,len,n,key,seq,val)')   :(new.end)

*   Construct new set with initial values determined by string _init_.

new

    kind = substr(init,1,3)
    init = substr(init,4)

*   Strip leading spaces from remaining part of _init_.

    init span(' ') =
    ne(size(kind),3) error('new missing kind specification')
    iskind(kind,'int','map','seq','set','str')         :s(new.init)
    error('bad kind to new ' kind )

    ident(kind,'int')                               :s(new.map)

new.init

*   Remove kind specification and following space from _init_

*   Create new instance of data type 'set'.

    new = set(kind,table(setl4.config.size), table(setl4.config.size))

*   Branch on set kind to finish initialization.

        :($('new.' kind))
    ident(kind,'int')                               :s(new.int)
    ident(kind,'map')                               :s(new.map)
    ident(kind,'seq')                               :s(new.seq)
    ident(kind,'set')                               :s(new.new)
    ident(kind,'str')                               :s(new.str)
    error('bad kind to new ' kind)

new.int

    ident(init) error('new initialization information needed')
    new = set('int')
    set.step(new) = 1
    seq = str.words(init)

*   Ignore extra arguments to int constructor.

    set.size(seq) = gt(set.size(seq),3) 3
    eq(set.size(seq),1)                             :s(new.int.1)
    eq(set.size(seq),2)                             :s(new.int.2)
    eq(set.size(seq),3)                             :s(new.int.3)

new.int.1

*   If one argument, low is one, high is the argument.

    set.low(new) =  1
    set.high(new) = seq.get(seq,1)            
    set.step(set) = 1
                                                    :(new.int.done)

new.int.2

*   If two arguments, low is low, high is second.

    set.low(new) =  +seq.get(seq,1)
    set.high(new) = +seq.get(seq,2)             
    set.step(set) = 1
                                                    :(new.int.done)

new.int.3

*   If three arguments, low is first, high is second, step is third.

    set.low(new) =  +seq.get(seq,1)
    set.high(new) = +seq.get(seq,2)             
    set.step(new) = +seq.get(seq,3)            
                                                    :(new.int.done)

new.int.done

*   Set _next_ so that  incrmenting it on first call to _next_  
*   will point to first element in set

    set.next(new) = set.low(new) - set.step(new) 
                                                    :(return)
new.map

*   If present, _init_ is a list consisting of a list of key/value pairs,
*   with a colon (:) between the key and value.

    ident(init)                                     :s(return)
    init = init ' '

new.map.next

    init break(':') . key ':' break(' ') . val span(' ') =  :f(return)
    map.put(new.map,key,val)                  :(new.map.next)

new.seq

    set.index(new) =
    set.key(new) =                        
    set.seq(new) = array(setl4.config.size)
                                                    :(return)

new.set

    ident(init)                                     :s(return)
    init = init ' '

new.set.init

    init break(' ') . element ' ' =                 :f(return)
    set.add(new,element)                            :(new.set.init)

new.str

    str.text(new) = init                            :(return)

new.str.init

    init len(1) . key ' ' =                         :f(return)
    setl.add(new,key)                               :(new.str.init)

new.end

    define('next(set)kind,next')                    :(next.end)

*   Advance iteration by setting field _set.next_.
*   Fail if no more elements remain.


next

    differ(kind = set.kind(set),'int')              :s(next.notint)

*   Here to find next element in set of type _int_.

    next = set.next(set) = set.next(set) + set.step(set)
    gt(next,set.high(set))                          :s(freturn)
    next = next                       
                                                    :(return)

next.notint

    next = set.next(set) = set.next(set) + 1
    le(next,set.size(set))                          :f(freturn)
    set.next(set) = 
.       (
.       iskind(kind,'int')          next,
.       iskind(kind,'map','set')    set.index(set)[next],
.       iskind(kind,'seq')          set.seq[next],
.       iskind(kind,'str')          substr(set.text(set),next,1)
.       ident(datatype(set),'string')   substr(set,next,1)
.       )
    next = set.next(set) = next 
                                                    :(return)

next.end

    define('not(expr)')                             :(not.end)

*   Returns not of boolean expression.

not

    expr = integer(expr) +expr
    not = integer(expr) (ne(expr) FALSE, TRUE)      :s(return)
    not = (differ(expr) FALSE, TRUE)                :(return)

not.end

    define('or(a,b)')                               :(or.end)

*   Returns true iff (if and only if) _a_ or _b_ is true.
*   Only evaluates _b_ if necessary

or
    or = integer(a) ne(a) TRUE                      :s(return)
    or = integer(b) ne(b) TRUE                      :s(return)

*   Here if operands not both integers, so use ident/differ.

    or = (differ(a) TRUE, differ(b) TRUE, FALSE)    :(return)

or.end

    define('random(n)')                             :(random.end)

*   Return integer uniformly distributed in 1,2,...,n.
*   If n=0 returns real uniformly distributed in the interval [0,1].
*   The algorithm can be found in section 16.1 of "Algorithms in SNOBOL4,"
*   James F. Gimpel, John Wiley and Sons, 1976.

random

    setl4.random_var = remdr(setl4.random_var * 4676, 414971)
    random  = setl4.random_var / 414971.0
    random = ne(n) convert(random * n,'integer') + 1
                                                    :(return)
random.end

    define('random.seed()s,c')                      :(random.seed.end)

*   Compute seed for random number generator from date and time.

random.seed
    setl4.random_var =
    s = g.date
    s = replace(s,'/:-','   ')

*   Get all the digits in s.

random.seed.loop

    s len(1) . c =                                  :f(random.seed.done)
    ident(c,' ')                                    :s(random.seed.loop)
    setl4.random_var = setl4.random_var c           :(random.seed.loop)

random.seed.done

*   Reverse the seed so digits depending on time come first.

    setl4.random_var = +reverse(setl4.random_var    )
                                                    :(return)
random.seed.end

    define('rank(set)ara,freq,i,entry,key,seq,str,value')    :(rank.end)

*   Rank returns a sorted version of its operand such that
*   elements are in increasing order. For example,
*           rank('asxybza') is 'aabsxyz'.
*   Sets are ranked in increasing order of their members.
*   Maps are ranked in increasing order of their keys.
*   Sequences (seq's) and int's are always maintained in rank order.

rank
 :(return)
    str.out('rank kind',set.kind(set))
    set = copy(set)
    iskind(set,'int','seq')                         :s(rank.seq)
    setl4kind(set,'map','set','str')                :s($('rank.' setl4kind(set)))
    ident(datatype(set),'string')                   :s(rank.string)
    error('rank unsupported operand type ' datatype(set) )

rank.map

    ara = sort(set.key,1)
    rank = map.new()

rank.map.next

    entry = ara[i = i + 1,1]                        :f(return)
    map.put(rank,key(entry),value(entry))           :(rank.map.next)

rank.seq
rank.int

*   Sequences and strings are always in rank order

                                                    :(return)

rank.set

    ara = sort(set.key,1)
    rank = set.new()

rank.set.next

    set.add(rank,ara[i = i + 1,1])                  :f(return)s(rank.set.next)

rank.str

    str = set.text(set)

rank.str.string

    freq = str.frequency(str)
    seq = set.new()

rank.str.collect

    str len(1) . i =                                :f(rank.str.set)
    set.add(seq,i)                                  :(rank.str.collect)

rank.str.set

    seq = rank(seq)
    loop(seq)

rank.str.next
    
    i = next(seq)                                   :f(return)
    rank.str = rank.str dupl(i,freq[i])             :(rank.str.next)

rank.string

    str = set                                       :(rank.str.string)

rank.end

*   Sequences:
*   ----------

*   A sequence, or 'seq', is a map defined on the positive integers
*   1,2, ... ,n. Sequences are implemented using SPITBOL arrays,
*   which are grown in size when adding a new element for which
*   no room remains in the array.


    define('seq.add(seq,val)')                      :(seq.add.end)

seq.add

    setl4.seq.room(seq)
    seq.add = seq.push(seq,val)                     :(return)

seq.add.end

    define('seq.get(seq,i)')                        :(seq.get.end)

*   Gets the value of element _i_ in the sequence _seq_

seq.get

    seq.get = set.seq(seq)[i]                       :f(freturn)s(return)

seq.get.end

    define('seq.integers(n)i')                      :(seq.integers.end)

*   Returns sequence of integers in the range 1 .. _n_.

seq.integers

    seq.integers = new('seq ' n)

seq.integers.next

    gt(i = i + 1, n)                                :s(return)
    setl4.seq.room(seq)
    seq.push(seq.integers,i)                        :(seq.integers.next)

seq.integers.end

    define('seq.push(seq,value)')                   :(seq.push.end)

seq.push

    assert.type(seq,'set')
    assert(ident(set.kind(seq),'seq'))
    value = integer(value) +value
    setl4.seq.room(seq)
    set.size(seq) = set.size(seq) + 1
    set.seq(seq)[set.size(seq)] = value             :(return)

seq.push.end

    define('seq.put(seq,i,value)v,len')             :(seq.put.end)

*   Sets the value of element _i_ in the sequence _seq_ to be _value_.

seq.put

    assert(ident(kind(seq),'seq'))
    set.seq(seq)[i] = value                         :(return)

seq.put.end

    define('seq.range(seq)')                        :(seq.range.end)


*    Return set with the values in _seq_ as its members.

seq.range

    kind = kind(seq)
    output = differ(kind,'seq') 'range called on non-seq, quitting' :s(end)
    seq.range = set.new()
    loop(seq)

seq.range.next

    set.add(seq.range,set.next(seq))                :f(return)s(seq.range.next)

seq.range.end


*   Sets
*   ____


    define('set.add(set,elem)')                     :(set.add.end)

*   Add _elem_ to set, unless already present.

set.add

    elem = integer(elem) +elem
    set.size(set) = set.size(set) + 1
    set.index(set)[set.size(map)] = elem
    set.key(set)[elem] = set.size(map)              :(return)

set.add.end

    define('set.difference(a,b)elem')               :(set.difference.end)

*   Returns set defined by set of elements of _a_ that are not in _b_.

set.difference

    set.difference = a @ !'set.member(=a,b)'       :(return)

set.difference.end

    define('set.first(set)')                       :(set.first.end)

*   Returns first element in _set_.

set.first

    set.first = set.key(set)[1]                    :(return)

set.first.end

    define('set.from(set,elem)this')               :(set.from.end)

*   Return set consisting of all the elements of _a_ except _elem_.

set.from

    set.from = set.copy(set)
    loop(set)

set.from.next

    this = next(set)                               :f(return)
    ident(this,elem)                               :s(set.from.next)
    set.add(set.from,this)                         :(set.from.next)

set.from.end

    define('set.integers(n)i')                      :(set.integers.end)

*   Return set containing integers 1 .. _n_

set.integers

    set.integers = set.new(n)
    i = 0

set.integers.next

    gt(i = i + 1,n)                                 :s(return)
    set.add(set.integers,i)                         :(set.integers.next)

set.integers.end

    define('set.intersection(a,b)elem')             :(set.intersection.end)

*   Returns intersection of sets _a_ and _b_.

set.intersection

*   Make _a_ the smaller set.

    le(set.size(a),set.size(b))                     :s(set.intersection.next)
    set.intersection = a
    a = b
    b = set.intersection
    set.intersectino = set.new('set')
    loop(a)

set.intersection.next

    next(a)                                         :f(return)
    set.member(=a,b)                                :f(set.intersection.next)
    set.add(set.intersection,=a)                    :(set.intersection.next)

set.intersection.end

    define('set.last(set)')                         :(set.last.end)

*   Returns last element in _set_.

set.last

    set.last = set.key(set)[set.size(set)]           :(return)

set.last.end


    define('set.subset(a,b)member')                 :(set.subset.end)

*   Tests if _b_ is a subset of _a_.

set.subset

    set.subset = FALSE
    gt(set.size(b),set.size(a))                     :s(return)
    loop(b)

set.subset.next

    member = set.next(b)                            :f(set.subset.true)
    member(member,a)                                :s(set.subset.next)f(return)

set.subset.true

    set.subset = true                               :(return)

set.subset.end

    define('set.union(a,b)elem')                    :(set.union.end)

*   Returns union of sets _a_ and _b_.

set.union

*   Arrange so set a is the larger set.

    ge(set.size(a),set.size(b))                     :s(set.union.a)
    set.union = a
    a = b
    b = set.union

set.union.a

*   Make copy of a by making copy of a's table.

    set.union = copy(a)
    loop(b)

set.union.b

    elem = set.next(b)                              :f(return)
    set.add(set.union,elem)                         :(set.union.b)

set.union.end

    define('show(v,label,lvl)i,kind,,ent,type,line,hdr')        :(show.end)

*   Show the value of expression _v_.
*   _lev_ is non-zero when called to list member or element
*   of a compound object.

show

    differ(label) str.out('show: ' str.upper(label))

*   If not set, branch on datatype, else branch on kind of set.

    type = datatype(v)
    differ(dt,'set')                                :($('show.' type))
                                                    :($('show.' set.kind(v)))
show.array

    proto = prototype(ara)
    proto break(',')                                :s(show.array.2)

*   Here for one dimensional array.

show.array.1
    first = ara[add1(.i)]                           :f(show.array.done)
    out(lpad(i,3) tab first)                        :(show.array.1)

show.array.2
    first = ara[add1(.i),1]                         :f(show.array.done)
    str.out(lpad(i,3) tab show(first) tab show(ara[i,2])) :(show.array.2)

show.entry

    show = show ' ' show(key(v))  ':' show(value(v)) ' ' :(show.done)

show.int

    show = 'integers(' int.low(v) '..' int.high(v) ') ' :(show.done)

show.integer

    show = v                                        :(show.done)

show.map

*   Call _rank_ so entries listed in rank order.

    map = rank(map)
    loop(v)
    show = ' {'

show.map.next

    member = next(v)                                :f(show.map.done)
    show = show ' ' show(member,,1)
    lt(set.size(show),100)                          :s(show.map.next)
    output = show ' '
    show =                                          :(show.map.next)

show.map.done

    show = show ' }'
                                                    :(show.done)

show.pair

    show = '{' lineid(num(pair)) ',' "'"
.                       replace(line(pair),' ','.') "'" '}' :(show.done)

show.seq

    str.out('show.seq')
    loop(v)
    show = ' ['

show.seq.next

    member = next(v)                                :f(show.seq.done)
    show = (ident(show) '[ ', show ' ') show(member,,1)
    lt(set.size(show),100)                          :s(show.seq.next)
    output = show ' '
    show =                                          :(show.seq.next)

show.seq.done

    show = show ' ]'                                :(show.done)

show.set

*   Call _rank_ so entries listed in rank order.

    set = rank(set)
    show =  ' {'
    loop(v)

show.set.next

    member = next(v)                                :f(show.set.done)

    show = (ident(show,'{') '{ ', show ' ') show(member,,1)
*   show = show ' '  show(member,,1)
    lt(set.size(v),100)                             :s(show.set.next)
    output = show
    show =                                          :(show.set.next)

show.set.done

    show = show ' }'
                                                    :(show.done)
show.str

    show = show.quote(str.text(set))                :(show.done)

show.string

*   Don't quote string that is an integer.

    show = integer(v) v                             :s(show.done)

    show = show.quote(v)                            :(show.done)


show.table

    t = convert(v,'array')

show.table.next

    key = t[i = i + 1,1]                            :f(show.done)
    value = t[i,2]

    output = tab show(key)    ' -> ' show(value)
                                                    :(show.table.next)
show.string.quote

    show = '"'  v '"'                               :(show.done)

show.done

    output = eq(lvl) show
                                                    :(return)

show.end
    
    define('show.b(e)')                             :(show.b.end)

*   Show value of _e_ as boolean: TRUE or FALSE

show.b

    integer(e) str.out((ne(e) 'TRUE', 'FALSE'))     :s(return)
    str.out((differ(e) 'TRUE', 'FALSE'))            :(return)

show.b.end

    define('show.line(line)')                       :(show.line.end)

*   Show a line with spaces replaced by '.'

show.line

    show.line = replace(line,' ','.')               :(return)

show.line.end

    define('show.lines(lines,title)i,line,titled')  :(show.lines.end)

*   Show the lines that are not empty in an array of lines

show.lines

    title  = 'lines' (differ(title) ' ' title, '')
    out(itle)

show.lines.n

    line = lines[add1(.i)]                          :f(return)
    empty(line)                                     :s(show.lines.n)
    differ(titled)                                  :s(show.lines.titled)

*   Do not list header unless at least one non-empty line

    out('    123456789012345')
    titled = 1

show.lines.titled

    out(lpad(lineid(i),2) ' ' replace(line,' ','.')) :(show.lines.n)

show.lines.end

    define('show.quote(str)')                       :(show.quote.end)

*   Show string _str_ enclosed in quotes

show.quote

    str break('"')                                  :s(show.quote.double)

    show.quote = '"' str '"'                        :(return)

show.quote.double

    show.quote = "'" str "'"                        :(return)

show.quote.end

    define('show.words(words)i,pos')                :(show.words.end)

*   Show results of words() call.

show.words

    pos = words[add1(.i),1]                         :f(return)
    out('words ' lpad(pos,2) ' ' words[i,2])        :(show.words)

show.words.end


*   Strings (str's)
*   ---------------

*   SETL4 provides additional operations, based on set theory,
*    on strings in the form of "str's".

    define('str.append(str,w,ch)')                  :(str.append.end)

*   _str.append_ appends _ch_, or ' ' if _ch_ is null, to _str_
*   and then appends _w_.

str.append

    str.append = str (ident(str) w, (ident(ch) ' ' , ch) w):(return)

str.append.end

    define('str.ascii(line)c,n,name,nul,num,pos')   :(str.ascii.end)

*   Return the text of line with every str.ascii character identified.

str.ascii

    true(setl4.ascii.initialized)                   :s(str.ascii.initialized)

*   Here to initialize the needed maps.
    
    setl4.ascii.init()
    setl4.ascii.initialized = true;

str.ascii.initialized

    nul = substr(&alphabet,1,1)

str.ascii.next

*   Add space after previous character if result not null.

    str.ascii = differ(str.ascii) str.ascii ' '
    c = substr(line,1,1)                            :f(str.ascii.done)
    ident(c,nul)                                    :s(str.ascii.null)
    line any('0123456789')                          :s(str.ascii.integer)
    line any(&lcase &ucase)                         :s(str.ascii.letter)
    line ' '                                        :s(str.ascii.space)

*   Check for control character (code <=26)

    &alphabet break(c) . pos                        :f(str.ascii.other)
    pos = size(pos)
    gt(pos,26)                                      :s(str.ascii.other)
*TODO here fix g. usage
    str.ascii = str.ascii g.str.ascii.name[c] '(^'
.               substr('ABCDEFGHIJKLMNOPQRSTUVWXYZ',pos,1) ')'
    line len(1) =                                   :(str.ascii.next)

str.ascii.integer

    line span('0123456789') . text =
    str.ascii = str.ascii  text                     :(str.ascii.next)

str.ascii.letter

    line span(&lcase &ucase) . text =
    str.ascii = str.ascii  text                     :(str.ascii.next)

str.ascii.null

    str.ascii = str.ascii 'NUL'                     :(str.ascii.next)

str.ascii.other

*   Here if none of the other cases apply, so just list the str.ascii name.

    str.ascii = str.ascii str.ascii.name[c]
    line = substr(line,2)                           :(str.ascii.next)

str.ascii.space

    line span(' ') . text =

    str.ascii =  str.ascii 'SPC'
    str.ascii = gt(size(text),1) str.ascii 'SPC*' size(text)
    str.ascii = str.ascii
                                                    :(str.ascii.next)
str.ascii.done

    str.ascii = trim(str.ascii)                     :(return)

str.ascii.end

    define('str.ascii.init(init)name,val')          :(str.ascii.init.end)

*   Initialize maps for use by _str.ascii_.
*   Since space (str.ascii 32) prints as space, use the name SPC for 32.

str.ascii.init


    str.ascii.init = setl4.str.map(
.       '0:NUL 16:DLE 32:SPC 48:0 64:@ 80:P  96:` 112:p '
.       '1:SOH 17:DC1 33:!   49:1 65:A 81:Q  97:a 113:q '
.       '2:STX 18:DC2 34:"   50:2 66:B 82:R  98:b 114:r '
.       '3:ETX 19:DC3 35:#   51:3 67:C 83:S  99:c 115:s '
.       '4:EOT 20 DC4 36:$   52:4 68:D 84:T 100:d 116:t '
.       '5:ENQ 21:NAK 37:%   53:5 69:E 85:U 101:e 117:u '
.       '6:ACK 22:SYN 38:&   54:6 70:F 86:V 102:f 118:v '
.       "7:BEL 23:ETB 39:'   55:7 71:G 87:W 103:g 119:w "
.       '8:BS  24:CAN 40:(   56:8 72:H 88:X 104:h 120:x '
.       '9:HT  25:EM  41:)   57:9 73:I 89:Y 105:i 121:y '
.       '10:LF 26:SUB 42:*   58:  74:J 90:Z 106:j 122:z '
.       '11:VT 27:ESC 43:+   59:; 75:K 91:[ 107:k 123:{ '
.       '12:FF 28:FS  44:,   60:< 76:L 92:\ 108:l 124:| '
.       '13:CR 29:GS  45:-   61:= 77:M 93:] 109:m 125:} '
.       '14:SO 30:RS  46:.   62:> 78:N 94:^ 110:n 126:~ '
.       '15:SI 31:US  47:/   63:? 79:O 95:_ 111:o 127:DEL ')

    str.ascii.name = str.map(efault.size)
    str.ascii.val = str.map(efault.size)


    loop(str.ascii.init)

str.ascii.init.next

    ent = setl4.str.next(str.ascii.init)                  :(str.ascii.init.finis)
*   Correct entry for 0 to be NUL, the NUL character
    str.ascii.name[substr(&alphabet,+v + 1,1)] = name
    str.ascii.val[name] = +val                    :(str.ascii.init.next)



    str.ascii.name[] = 'NUL'
    str.ascii.val['NUL'] = 0
    str.ascii.name[' '] = 'SPC'
    str.ascii.val['SPC'] = 32                       :(return)

str.ascii.init.end

    define('str.datename(date)s')                  :(str.datename.end)

*   Convert date to form with no spaces, just dashes, to be
*   suitable for use as part of a file name.

str.datename

    str.datename = replace(date,' :/','---')
    str.datename = substr(str.datename,12,8) '-' substr(str.datename,1,10)
                                                   :(return)
str.datename.end

    define('str.digits()str,ch')                    :(str.digits.end)

*   Return string consisting of the digits 0 .. 9 separated by a space.

str.digits

    str.digits = 
    str = '0123456789'
    
str.digits.next

    str len(1) . ch =                               :f(return)
    str.digits = str.append(str.digits,ch)          :(str.digits.next)

str.digits.end

    define('str.frequency(str)')                    :(str.frequency.end)

*   Return map from the characters in a str to the number of times
*   each occurs in the str.

str.frequency

    str.frequency = map.new()

str.frequency.next

    str len(1) . c =                                :f(str.frequency.end)
    setl4.str.put(str.frequency,c,setl4.str.get(str.frequency,c) + 1)   :(str.frequency.next)

str.frequency.end

    define('str.less(str,sub)before,after')         :(str.less.end)

*   _Less_ removes the first instance of each character in _sub_ from _str_,
*   where _sub_ is a substr of _str_.

str.less

    ident(str)                                      :s(return)
    str.less = str

str.less.ch
    sub len(1) . ch =                               :f(return)
    str.less break(ch) . before  ch rem . after
    str.less = before after                         :(str.less.ch)

str.less.end

    define('str.lletters()str,ch')                  :(str.lletters.end)

*   Return string consisting of the lower case letters separated by a space.

str.lletters

    str.lletters = 
    str = &lcase
    
str.lletters.next

    str len(1) . ch =                              :f(return)
    str.lletters = str.append(str.lletters,ch)     :(str.lletters.next)

str.lletters.end

    define('str.letters()str,ch')                  :(str.letters.end)

*   Return string consisting of the lower case letters separated by a space.

str.letters

    str.letters = 
    str = &lcase &ucase
    
str.letters.next

    str len(1) . ch =                              :f(return)
    str.letters = str.append(str.letters,ch)       :(str.letters.next)

str.letters.end

    define('str.lower(s)')                          :(str.lower.end)

*   Convert argument to lower case.

str.lower

    str.lower = replace(s,&ucase,&lcase)            :(return)

str.lower.end

    define('str.out(text1,text2,text3)type1,type2') :(str.out.end)

*   Output a line conisting of TEXT1, TEXT2 enclosed
*   in brackets, followed by TEXT3.

str.out

    output = text1 (differ(text2) '[' text2 ']', '') (differ(text3) text3, ''):(return)

str.out.end

    define('str.prefix(str,pre)word')               :(str.prefix.end)

*   Prefix takes a list of space-separated words str and str.prefixes each
*   with the str pre.

str.prefix

    str = str ' ';* so each word followed by space

str.prefix.1

    str break(' ') . word span(' ')  =              :f(return)
    add1(.str.prefixcount)
    str.prefix = append(str.prefix, pre word,'A')   :(str.prefix.1)

str.prefix.end

    define('str.uletters()str,ch')                  :(str.uletters.end)

*   Return string consisting of the upper case letters separated by a space.

str.uletters

    str.uletters = 
    str = &ucase
    
str.uletters.next

    str len(1) . ch =                                :f(return)
    str.uletters = str.append(str.uletters,ch)       :(str.uletters.next)

str.uletters.end

*   Lexical Comparison Functions
*   ----------------------------

*   The following lexical comparison functions return TRUE or FALSE.
*   They are needed since the SPITBOL functions signal the result by success or failure.

    define('str.leq(a,b)')                           :(str.leq.end)

*   Tests if _a_ is lexically equal to _b_.

str.leq

    str.leq = leq(a,b) true                         
    str.leq = (leq(a,b) TRUE, FALSE) :s(return)
          
str.leq.end

    define('str.lge(a,b)')                           :(str.lge.end)

*   Tests if _a_ is lexically greater than or equal to _b_.

str.lge

    str.ge = (ge(a,b) TRUE, FALSE) :s(return)

str.lge.end

    define('str.lgt(a,b)')                           :(str.lgt.end)

*   Tests if _a_ is lexically greater than _b_.

str.lgt

    str.lgt = (gt(a,b) TRUE, FALSE) :s(return)

str.lgt.end

    define('str.lle(a,b)')                           :(str.lle.end)

*   Tests if _a_ is lexically less than or equal to _b_.

str.lle

    str.le = (le(a,b) TRUE, FALSE)                   :s(return)

str.lle.end

    define('str.llt(a,b)')                           :(str.llt.end)

*   Tests if _a_ is lexically less than _b_.

str.llt

    str.llt = (llt(a,b) TRUE, FALSE) :s(return)

str.llt.end

    define('str.lne(a,b)')                           :(str.lne.end)

*   Tests if _a_ is not lexically equal to _b_.

str.lne

    str.lne = (ne(a,b) TRUE, FALSE) :s(return)

str.lne.end


*   Lines
*   -----

*   Lines can be just strs or, in some cases, a line is represented by a pair:

    data('line(mark,text)')

*   where

*           _mark_ is defined by the value returned by an instance
*           of the _eval_ function ...

*           _text_ is the text of the line.

*   For example, the _reader_ function reads a file and returns a
*   sequence of _lines_.

    define('str.reader(filename,expr)line,mark')    :(str.reader.end)

*   Return a sequence of the lines in the file specified by _filename_.
*   If _expr_ is not null, then _eval_ is called just after each
*   line is read, with the variable _pair_ setl4.str to the associated pair.
*   If _eval_ fails the line is skipped.  Otherwise returns a new pair
*   to be entered in the input seq.

str.reader

    differ(filename) input(.file,g.channel.temp,filename):s(str.reader.opened)
    out('unable to open str.reader file ' filename '.') :(freturn)

str.reader.opened

    str.reader = map(1024)

str.reader.next

    setl4.str.add(map, (ident(filename) input, file)) :f(str.reader.eof)s(str.reader.next)

str.reader.eof

*   Here at end of file; close temporary file if one was needed.

    differ(filename) endfile(g.channel.temp)        :(return)

str.reader.end

    define('str.reader.str(str,delim)line')         :(str.reader.str.end)

*   Returns a sequence of the lines in _str_,
*   where the delimiter character _delim_ is used to mark
*   the end of a line. One intended use of _str.reader.str_
*   is to assist in the construction of test programs.

str.reader.str

    delim = ident(delim) '/'
    str.reader.str = setl4.str.seq()

str.reader.str.next

    str break(delim) . line delim =                 :f(return)
    add(str.reader.str,line)                        :(str.reader.str.next)

str.reader.str.end

    define('str.set(str)')              :(str.set.end)

*   Returns setl4.str of characters in str _str_.

str.set

    str.str = setl4.str(size(str))
    loop(str)

str.set.next

    next(str.set)                   :f(return)
    setl4.str.add(str.set,next(set))                :(str.set.next)

str.set.end

    define('str.slice(str,first,last)')             :(str.slice.end)

str.slice

    str.slice = substr(str,first,last)              :(return)

str.slice.end

*   Tokens: Lexical Scanning
*   ------------------------


*   A token is represented as an instance of the datatype _token_ as follows:

    data('token(lineno,ndx,type,text)')

*   where:
*           _lineno_        is the line number within the file;
*           _ndx_           is the index of the first character of the
*                           token in the line;
*           _type_          is the token's type, as described below; and
*           _text_          is the text of the token.

*   The type is represented by a single character, as follows:

*           'c'             comment, indicated by asterisk (*) in the first column.
*                           The text consists of the entire line.

*           'i'             integer, consisting of one or more digits ('0123456789')

*           'l'             left opener, one of '(<[{'

*           'p'             punctation, one of '.;,?!', or single quote ('),
*                           or double quote ("),

*           'o'             other printable character, one of '~@#$%^&*_-+=`'.

*            'q'            quoted str, starting witn '"', and continuing
*                           to the next instance of the opening quote character.

*           'r'             right closer, one of ')>]}'

*           's'             space, consistine of one or more spaces (spaces)

*           't'             tab character

*           'u'             unprintable character, for example control-k

*           'w'             word, consisting of one of more letters in
*                           upper or lower case or "'" or "_".

*   The longest possible str meeting the rules is used when building a token.
*   For example, 'abc' is just the word 'abc', not 'a' followed by 'bc', and so forth.

*   It is recommended that the tab character NOT be used as an abbreviation for
*   one or more spaces in the input file. If tabs are used, then _pos_ is ill-defined.
*   If tabs are not used, then _pos_ indicates the true position in the line of
*   the first character of the token, and so error messages and references to the
*   token will be exact.  For example, _pos_ could be used to indicate where to give
*   emphasis to a token by underlining it, or enclosing it in '_' to generate
*   markdown format, and so forth.

    define('str.tokens(line)type,pos,text,lineno')  :(str.tokens.end)

*   Str.tokens scans _line_ and returns a table of the str.tokens in the line.
*   The table has keys from 1..n, where _n_ is the number of str.tokens found.

*   This function is an instance of what is called a lexiical scanner, or tokenizer,
*   which is the first stage in a programming language compiler, or for any program
*   that processes text files with a specified structure.

str.tokens

    str.tokens = setl4.str.seq()
    line '*'                                        :f(str.tokens.next)
    type = 'c'
    text = line
    line =                                          :(str.tokens.new)

str.tokens.next

    line ' '                                        :s(str.tokens.space)
    line any('0123456789')                          :s(str.tokens.integer)
    line any('(<[{')                                :s(str.tokens.left)
    line any('.;,?!')                               :s(str.tokens.punctuation)
    line '"'                                        :s(str.tokens.quote)
    line any('~@#$%^&*_-+=')                                        :s(str.tokens.other)
    line any(')>]}')                                :s(str.tokens.right)
    line char(9)                                    :s(str.tokens.tab)
    line any(&lcase &ucase)                         :s(str.tokens.word)

*   Here for unprintable
                                                    :(str.tokens.unprintable)

str.tokens.integer

    type = 'i'
    line span('0123456789') . text =                :(str.tokens.new)

str.tokens.punctuation

    type = 'p'                                       :(str.tokens.character)

str.tokens.other

    type = 'o'                                      :(str.tokens.character)

*str.tokens.other

    type = 'o'                                      :(str.tokens.character)

str.tokens.quote

    type = 'q'
    line '"' break('"') . text '"' =
    text = '"' text '"'                             :(str.tokens.new)

str.tokens.right

    type = 'r'                                      :(str.tokens.character)

str.tokens.space

    type = 's'
    line span(' ')  . text =                        :(str.tokens.new)

str.tokens.tab

    type = 't'                                      :(str.tokens.character)

str.tokens.word

*FIX*
    type = 'w'
    line span(&ucase &lcase "_" "'") . text =       :(str.tokens.new)

str.tokens.character

*   Here if token is single character.

    line len(1) . text                              :(str.tokens.new)

str.tokens.new

    setl4.str.add(str.tokens,token(lineno,pos,type,text)) :(str.tokens.next)

str.tokens.end

    define('str.upper(s)')                          :(str.upper.end)

*   Convert argument to upper case.

str.upper

    str.upper = replace(s,&lcase,&ucase)            :(return)

str.upper.end

    define('str.words(str)n,tmp,w')                 :(str.words.end)

*   Return sequence consisting of the words in _str_, where the 
*   words are separated by one or more consecutive spaces and tabs.

str.words

*   Make first pass over string to get number of words, _n_.

    str = str ' '
    tmp = str

str.words.count

    tmp setl4.break.whitespace . w setl4.span.whitespace  = :f(str.words.seq)
    n = n + 1                                       :(str.words.count)

str.words.seq

    str.words = new('seq ' n)
    
str.words.next

    str setl4.break.whitespace . w setl4.span.whitespace  = :f(return)
    seq.push(str.words,w)                           :(str.words.next)

str.words.end

    define('str.writer(lines)')                    :(str.writer.end)

*   Writes the lines in the sequence _lines_ to standard output.

str.writer

    loop(lines)

str.writer.next

    output = set.next(lines)                        :f(return)s(str.writer.next)

str.writer.end

    define('this(set)')                       :(this.end)

*   Returns value found in most recent call to next.

this

    this = set.next(set)                       
*    show(this)
                                                    :(return)

this.end

    define('true(e)')                              :(true.end)

*   Returns TRUE if _e_ is true, or else FALSE.

true

    e = integer(e) +e
    true = integer(e) (ne(e) TRUE,FALSE)            :s(return)
    true = (differ(e) TRUE, FALSE)                  :(return)

true.end

    define('visit(set,expr)')                       :(visit.end)

*   _Visit_ loops over _set and calls _eval_ with argument
*   _expr_  for each of its members.

visit

    loop(set)

visit.next

    next(set)                                       :f(return)
    eval(expr)                                      :(visit.next)

visit.end

    define('setl4.seq.room(seq)len')                :(setl4.seq.room.end)

*   Ensures that sequence _seq_ has room for a new entry, expanding it if necessary.

setl4.seq.room

    len = prototype(set.seq(seq))
    le(set.size(seq),len)                           :s(return)

*   Here to extend length of array by fifty per cent.

    setl4.seq.extend(seq,(3 * len) / 2)             :(return)

setl4.seq.room.end
    
    define('setl4.show(set,label)')                 :(setl4.show.end)

*   Show the fields of an instance of _set_.

setl4.show

                               str.out('{')
    differ(label)              str.out('show: ' str.upper(label))
                               str.out('kind',set.kind(set))
    ne(set.size(set))          str.out('size',set.size(set))
    differ(set.index(set))     str.out('size(set.index(set))', size(set.index(set)))
    differ(set.key(set))       str.out('size(set.key(set))', size(set.key(set)))
    differ(set.next(set))      str.out('next',set.next(set))
    ne(set.low(set))           str.out('low',set.low(set))
    ne(set.step(set))          str.out('high',set.high(set))
    ne(set.high(set))          str.out('step',set.low(set))
    differ(set.text(set))      str.out('text',set.text(set))
    differ(set.seq(set))       str.out('seq',prototype(set.seq(set)))
                               str.out('}')

                                                    :(return)
setl4.show.end

    define('setl4.size(set)')                       :(setl4.size.end)

*   Return size of operand.

setl4.size

    setl4.size = ident(datatype(set),'string') size(set)    :s(return)
    kind = iskind(set)
    setl4.size = iskind(kind,'int')
.               set.high(set) - set.low(set) + 1    :s(return)
    setl4.size = set.size(set)                      :(return)

setl4.size.end

    define('split(str)word')                  :(split.end)

*   Returns sequence of the words in stirng _str_.
*   Words are separated by whitespace: one of more consecuture spaces
*   or tabs.

split

    split = new('seq')
    line =  line ' '

split.next

    line break(' ') . word ' ' =                    :f(return)
    set.add(split,word)                             :(split.next)

split.end

    define('setl4.traceoff()')                      :(setl4.traceoff.end)

*   Initiate tracing.

setl4.traceoff
    &fsetl4.trace = &setl4.trace =

    g.scoring = g.tracing = 0
    out('stop TRACING ')

setl4.traceoff.end

    define('setl4.traceon()')                       :(setl4.traceon.end)

*   Initiate tracing.

*TRACEON*
setl4.traceon

    out('start TRACING')
*   &fsetl4.trace = &setl4.trace = 1500000
    g.scoring = g.tracing = 1
                                                    :(return)
setl4.traceon.end

*   SETL4 uses some of the undefined SPITBOL operators for SETL4 functions.

*   Characters available for use by 'opsyn' to associate the character
*   with a function.

*   binary  &  @  #  %  ~
*   unary:  !  %  /  #  =  | /

*   define('setl4.op(ch,proc,n)')                   :(setl4.op.end)
*
**  Use _opsyn_ to define SETL4 operator.
*
*setl4.op
*
*   opsyn(ch,proc,n)                                :s(return)
*   error('fatal problem with opsyn for ' ch ')
*
*setl4.op.end

*   SETL4 binary operators

    opsyn('&','and',        2)
    opsyn('@','at',         2)
    opsyn('#','filter',     2)
    opsyn('~','member',     2)
*   opsyn('^','or',        2)

*   SETL4 unary operators:

    opsyn('=','this',       1)
    opsyn('|','new',        1)
    opsyn('!','not',        1)
    opsyn('#','setl4.size', 1)
    opsyn('/','split',      1)

*   Here endeth the SETL4 code.
*   May you fare well using it.

